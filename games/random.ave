== Welcome to Lower Mudville ==
-- Explore this randomly generated village. What will you discover? --
** Matthew Scroggs **
@@ 3 @@
vv 1 vv
:: 1.9 ::

# start
You are standing in the road near a sign that says "Welcome to Lower Mudville".
Enter Lower Mudville => generate_street

# generate_street
+ tired
~ street:bin ~ street:crisps ~ street:police
~ street:left:wall ~ street:left:postoffice ~ street:left:pub ~ street:left:field ~ street:left:house
~ street:right:villagegreen ~ street:right:blacksmith ~ street:right:school ~ street:right:shop ~ street:right:house ~ street:right:villagehall
~ leftgen ~ rightgen
+ street:bin ? __R__>0.8
+ street:crisps ? __R__>0.5
+ street:police ? __R__>0.9
+ street:police ? good<=-15 ? __R__>0.1
+ street:left:house ? __R__>0.8
+ street:left:field ?! street:left:house ? __R__>0.75
+ street:left:pub ?! street:left:field ?! street:left:house ? __R__>0.6666
+ street:left:postoffice ?! street:left:pub ?! street:left:field ?! street:left:house ? __R__>0.5
+ street:left:wall ?! street:left:postoffice ?! street:left:pub ?! street:left:field ?! street:left:house ? __R__>0.1
+ street:right:villagehall ? (good>=13 good<=-13) ? __R__>0.1
+ street:right:villagehall ? __R__>0.9
+ street:right:house ?! street:right:villagehall ?! street:left:house ? __R__>0.8
+ street:right:shop ?! street:right:house ?! street:right:villagehall ? __R__>0.7
+ street:right:school ?! street:right:shop ?! street:right:house ?! street:right:villagehall ? __R__>0.6
+ street:right:blacksmith ?! street:right:school ?! street:right:shop ?! street:right:house ?! street:right:villagehall ? __R__>0.5
+ street:right:villagegreen ?! street:right:blacksmith ?! street:right:school ?! street:right:shop ?! street:right:house ?! street:right:villagehall ? __R__>0.4
You walk along the street.
Continue => street

# street
You are standing in the street.
There is a bin on the pavement near you. ? street:bin
There is a crisp packet on the road next to you. ? street:crisps
There is a police officer nearby. ? street:police
There is a house to your left. ? street:left:house
There is a field to your left. ? street:left:field
There is a wall to your left. ? street:left:wall
There is a post office to your left. ? street:left:postoffice
There is a pub to your left. ? street:left:pub
There is a village hall to your right. ? street:right:villagehall
There is a house to your right. ? street:right:house
There is a village green to your right. ? street:right:villagegreen
There is a blacksmith's shop to your right. ? street:right:blacksmith
There is a school to your right. ? street:right:school
There is a shop to your right. ? street:right:shop
You are very tired. ? tired>5
Put the crisp packet(s) in the bin => street ? street:bin ? crisp_packet + good+crisp_packet + crisp_packet=0
Pick up the crisp packet => street ? street:crisps + crisp_packet ~ street:crisps
Walk along the street => generate_street ? tired<=5
Walk along the street => tootired ? tired>5
Go into the house => generate_house ? street:left:house ?! leftgen + leftgen
Go into the field => generate_field ? street:left:field ?! leftgen + leftgen
Go to the wall => generate_wall ? street:left:wall ?! leftgen + leftgen
Go into the post office => generate_postoffice ? street:left:postoffice ?! leftgen + leftgen
Go into the pub => generate_pub ? street:left:pub ?! leftgen + leftgen
Go into the village hall => generate_villagehall ? street:right:villagehall ?! rightgen + rightgen
Go into the house => generate_house ? street:right:house ?! rightgen + rightgen
Go to the village green => generate_villagegreen ? street:right:villagegreen ?! rightgen + rightgen
Go to the blacksmith's shop => generate_blacksmith ? street:right:blacksmith ?! rightgen + rightgen
Go to the school => generate_school ? street:right:school ?! rightgen + rightgen
Go to the shop => generate_shop ? street:right:shop ?! rightgen + rightgen
Go back into the house => house ? street:left:house ? leftgen
Go back into the field => field ? street:left:field ? leftgen
Go back to the wall => wall ? street:left:wall ? leftgen
Go back into the post office => postoffice ? street:left:postoffice ? leftgen
Go back into the pub => pub ? street:left:pub ? leftgen ?! pub:barred
Go back into the village hall => villagehall ? street:right:villagehall ? rightgen
Go back into the house => house ? street:right:house ? rightgen
Go back to the village green => villagegreen ? street:right:villagegreen ? rightgen
Go back to the blacksmith's shop => blacksmith ? street:right:blacksmith ? rightgen
Go back to the school => school ? street:right:school ? rightgen
Go back to the shop => shop ? street:right:shop ? rightgen
Have a nap in the street => streetnap ? tired>5
Talk to the police officer => policetalk ? street:police

# tootired
You are too tired to walk.
Continue => street

# generate_wall
~ wall:unclean ~ wall:dented ~ wall:noticeboard
~ wall:unclean ? __R__>0.6
~ wall:noticeboard ? __R__>0.6
You walk over to the wall.
Continue => wall

# wall
You are standing by a wall.
The wall is covered in grafitti. ? wall:unclean
The wall is covered in dents made by a hammer. ? wall:dented
There is a notice board on the wall. ? wall:noticeboard ? wall:noticeboardfixed
The noticeboard is only attached by one corner. ? wall:noticeboard
Wash the wall => wash ? soap ? bucket ? sponge ? wall:unclean ~ wall:unclean
Reattach the notice board to the wall => wall ? wall:noticeboard ~ wall:noticeboard + wall:noticeboardfixed ? hammer ? nails>3 + nails-3
Cover the wall in grafitti => wall ? paint ?! wall:unclean + wall:unclean + good-7 ~ paint
Put dents in the wall with the hammer => wall ? hammer ?! wall:dented + wall:dented + good-5
Go back to the street => street 

# wash
You use the soap, bucket and sponge to wash the wall. + good+5
Continue => wall

# policetalk
"Good day!" says the police officer. ? good>-10 + good
"I'm looking for ? good>-10 ? (!police:hammer !police:nails)
a hammer ? good>-10 ?! police:hammer
and ? good>-10 ?! police:hammer ?! police:nails
some nails ? good>-10 ?! police:nails
to fix the village notice board. Do you have any?" ? good>-10 ? (!police:hammer !police:nails)
"You've been making a mess of our village," say the police officer. "You are under arrest." ? good<=-10
Stop talking to the police officer => street ? good>-10
Give the police officer a hammer => street ? good>-10 ?! police:hammer + police:hammer ? hammer ~ hammer + good+5
Give the police officer some nails => street ? good>-10 ?! police:nails + police:nails ? nails>3 + nails=0 + good+5
Continue => arrest ? good<=-10

# arrest
You are in prison.
Continue => __GAMEOVER__

# streetnap
You have a nap in the road. + tired=0
You wake up to find there is a queue of cars waiting for you to move. The drivers look annoyed. + good-4 ? __R__>0.3
Continue => street

# generate_villagehall
~ vhall:meeting
+ vhall:meeting ? __R__>0.2
You walk into the village hall.
Continue => villagehall

# villagehall
You are standing in the village hall.
There is a parish council meeting taking place. ? vhall:meeting
Talk to the parish councillors => council ? vhall:meeting
Go back to the street => street

# council
As you approach the councillors, one of them points at you. ? (good>=10 good<=-10)
"You have done many kinds deeds for this village. I nominate you for Mayor." ? good>=10
"You have been wreaking havoc in our village. You are banished!"" ? good<=-10
"Can't you see we're busy," barks one of the councillors, "you'll have to come back later." ? good<10 ? good>-10 ~ good
Go back to the street => street ? good<10 ? good>-10
Continue => mayor ? good>=10
Continue => banished ? good<=-10

# mayor
You are mayor of Lower Mudville.
Continue => __WINNER__

# banished
You are banished from Lower Mudville.
Continue => __GAMEOVER__

# generate_house
~ house:angry ~ house:toolbox ~ house:toolhammer ~ house:tea ~ house:toolsaw ~ house:toolnails
~ house:toolscrewdriver
~ house:brokentable ~ house:wallet ~ house:moneytaken
~ house:soap ~ house:bucket ~ house:sponge
~ house:armchair ~ house:sofa ~ house:tv ~ house:table ~ house:smashtv
+ house:angry ? __R__>0.9
+ house:wallet ? __R__>0.2 ?! house:angry
+ house:armchair ? __R__>0.2 ?! house:angry
+ house:sofa ? __R__>0.2 ?! house:angry
+ house:tv ? __R__>0.2 ?! house:angry
+ house:table ? __R__>0.2 ?! house:angry
+ house:tea ? __R__>0.75 ?! house:angry
+ house:toolbox ? __R__>0.7 ?! house:angry
+ house:toolhammer ? __R__>0.4 ? house:toolbox ?! house:angry
+ house:toolsaw ? __R__>0.4 ? house:toolbox ?! house:angry
+ house:toolnails ? __R__>0.4 ? house:toolbox ?! house:angry
+ house:toolscrewdriver ? __R__>0.4 ? house:toolbox ?! house:angry
+ house:soap ? __R__>0.5 ?! house:angry ?! soap
+ house:bucket ? __R__>0.5 ?! house:angry ?! bucket
+ house:sponge ? __R__>0.5 ?! house:angry ?! sponge
You walk into the house.
Continue => house

# house
You are standing in a house.
There is a table in the house. ? house:table
There are some broken pieces of a table in the house. ? house:brokentable
There is an armchair in the corner. ? house:armchair
There is a sofa in the room. ? house:sofa
There is a TV in the house. ? house:tv
There is a smashed TV in the house. ? house:smashtv
The owner of the house angrily tells you to get out of their house. ? house:angry ~ good
There is a cup of tea on the table. ? house:tea ? house:table
There is a box of soap on the table. ? house:soap ? house:table
There is a bucket by the table. ? house:bucket ? house:table
There is a sponge on the table. ? house:sponge ? house:table
There is a toolbox on the table. ? house:toolbox ? house:table
There is a wallet on the table. ? house:wallet ? house:table ?! house:moneytaken
There is an empty wallet on the table. ? house:wallet ? house:table ? house:moneytaken
There is a cup of tea on the floor. ? house:tea ?! house:table
There is a box of soap on the floor. ? house:soap ?! house:table
There is a bucket on the floor. ? house:bucket ?! house:table
There is a sponge on the floor. ? house:sponge ?! house:table
There is a toolbox on the floor. ? house:toolbox ?! house:table
There is a wallet on the floor. ? house:wallet ?! house:table ?! house:moneytaken
There is an empty wallet on the floor. ? house:wallet ?! house:table ? house:moneytaken
Drink the tea => drinktea ? house:tea
Take money from the wallet => house + money+20 + good-7 ? house:wallet ?! house:moneytaken + house:moneytaken
Pick up the soap => house ? house:soap ~ house:soap + soap
Pick up the bucket => house ? house:bucket ~ house:bucket + bucket
Pick up the sponge => house ? house:sponge ~ house:sponge + sponge
Look in the toolbox => toolbox ? house:toolbox
Cut a leg off the table => legless ? house:table ? saw
Have a nap on the sofa => house ? house:sofa ? tired>0 + tired=0
Have a nap on the armchair => house ? house:armchair ? tired>0 + tired=0
Hit the TV with the hammer => house ? house:tv ~ house:tv + house:smashtv + good-3 ? hammer
Go back to the street => street

# legless
You cut a leg off the table, the table tips over and cracks in half. + good-5 + house:brokentable ~ house:table
The cup of tea spills ? house:tea ~ house:tea
Continue => house

# toolbox
You look in the toolbox.
It is empty. ?! house:toolhammer ?! house:toolsaw ?! house:toolnails ?! house:toolscrewdriver
There is a hammer. ? house:toolhammer
There is a saw. ? house:toolsaw
There are some nails. ? house:toolnails
There is a screwdriver. ? house:toolscrewdriver
Pick up the hammer => toolbox ? house:toolhammer ?! hammer + hammer ~ house:toolhammer
Pick up the saw => toolbox ? house:toolsaw ?! saw + saw ~ house:toolsaw
Pick up the nails => toolbox ? house:toolnails + nails=10 ~ house:toolnails
Pick up the screwdriver => toolbox ? house:toolscrewdriver ?! screwdriver + screwdriver ~ house:toolscrewdriver
Continue => house

# drinktea
You drink the tea. It is delicious (but a little cold). ~ house:tea + tired-5
+ tired=0 ? tired<0
Continue => house

# generate_field
~ field:scarecrow ~ field:buckethead ~ field:scarecrowcutdown ~ field:crisps ~ field:crows
+ field:scarecrow ? __R__>0.6
+ field:crows ?! field:scarecrow ? __R__>0.3
You walk into the field.
Continue => field

# field
You are standing in a field.
There are crows in the field eating the crops. ? field:crows
There is a crisp packet in the field next to you. ? field:crisps
There is a scarecrow standing in the field. ? field:scarecrow ?! field:scarecrowcutdown
There is a scarecrow lying in the field. ? field:scarecrow ? field:scarecrowcutdown
There is bucket on the scarecrow's head. ? field:scarecrow ? field:buckethead
Pick up the crisp packet => field ? field:crisps ~ field:crisps + crisp_packet + good
Put the bucket on the scarecrow's head => field ? field:scarecrow ? bucket ~ bucket + field:buckethead ?! field:buckethead + good-3
Use the saw to cut down the scarecrow => field ? field:scarecrow ? saw ?! field:scarecrowcutdown + field:scarecrowcutdown + good-6
Chase the crows away => field ? field:crows ~ field:crows + good+3
Go back to the street => street

# generate_pub
~ pub:barred
~ pub:oldmuddy ~ pub:mudvillegold ~ pub:mild
+ pub:oldmuddy ? __R__>0.3
+ pub:mudvillegold ? __R__>0.3
+ pub:mild ? __R__>0.3
You walk into the pub.
Continue => pub

# pub
You are in the pub.
But a pint of Old Muddy for <money>3 => oldmuddy ? pub:oldmuddy ? money>=3 + money-3
But a pint of Mudville Gold for <money>3 => mudvillegold ? pub:mudvillegold ? money>=3 + money-3
But a pint of Born to be Mild for <money>2 => mild ? pub:mild ? money>=2 + money-2
Saw the bar in half => __R__(sawbargood,sawbarbad) ? saw
Go back to the street => street

# sawbargood
The landlord look at you, confused.
<newline><newline>
"How on Earth did you know that I needed to cut the bar there to get the new fridge in?!" says the landlord.
"You've saved me having to hire someone to do that, take this."
<newline><newline>
The landlord gives you <money>10. + money+10 + good+2
Continue => pub

# sawbarbad
The landlord look at you, confused.
<newline><newline>
"What on Earth are you doing?! You're barred!" + good-3 + pub:barred
Continue => street

# oldmuddy
You drink a pint of Old Muddy and listen to villagers talk about how the parish council are looking to elect a new mayor of Mudville.
Continue => pub

# mudvillegold
You drink a pint of Mudville Gold and listen to villagers talk about how the village policeman has been trying to fix the noticeboards in the village.
Continue => pub

# mild
You drink a pint of Born to be Mild and listen to villagers talk about the village school needing to raise lots of money for roof repairs.
Continue => pub

# generate_postoffice
~ po:crisps ~ po:soap ~ po:paint ~ po:scratchcard
+ po:crisps ? __R__>0.2
+ po:soap ? __R__>0.2
+ po:paint ? __R__>0.2
You walk into the post office.
Continue => postoffice

# postoffice
You are in the post office.
Post a letter to your friend (for <money>1) => postoffice ? money>=1 + money-1 + good
Buy a packet of crisps for <money>1 => pocrisps ? po:crisps ? money>=1 + money-1
Buy a box of soap for <money>2 => postoffice ? po:soap ?! soap ? money>=2 + money-2
Buy some spray paint <money>2 => postoffice ? po:paint ?! paint ? money>=2 + money-2
Buy a scratchcard for <money>1 => posc ? money>=1 + money-1
Go back to the street => street

# posc
You scratch off the windows on the scratchcard...
Continue => __R__(posc_big_win,posc_win,posc_lose)[1,10,100]

# posc_big_win
You win <money>100! + money+100
Continue => postoffice

# posc_win
You win <money>10! + money+10
Continue => postoffice

# posc_lose
You didn't win a prize this time.
Continue => postoffice

# pocrisps
You eat the crisps. + crisp_packet
Continue => postoffice

# generate_shop
~ shop:hammer ~ shop:saw ~ shop:nails ~ shop:screwdriver ~ shop:bucket ~ shop:sponge ~ shop:soap ~ shop:paint
+ shop:hammer ? __R__>0.4
+ shop:saw ? __R__>0.4
+ shop:nails ? __R__>0.4
+ shop:screwdriver ? __R__>0.4
+ shop:bucket ? __R__>0.4
+ shop:sponge ? __R__>0.4
+ shop:soap ? __R__>0.4
+ shop:paint ? __R__>0.4
You walk into the shop.
Continue => shop

# shop
You are in a shop.
Put <money>1 in the charity donation box => shop ? money>=1 ~ money + good
Buy a hammer for <money>1 => shop ? shop:hammer ?! hammer + hammer ? money>=1 + money-1
Buy a saw for <money>2 => shop ? shop:saw ?! saw + saw ? money>=2 + money-2
Buy 10 nails for <money>1 => shop ? shop:nails + nails+10 ? money>=1 + money-1
Buy 25 nails for <money>2 => shop ? shop:nails + nails+25 ? money>=2 + money-2
Buy a screwdriver for <money>1 => shop ? shop:screwdriver ?! screwdriver + screwdriver ? money>=1 + money-1
Buy a bucket for <money>2 => shop ? shop:bucket ?! bucket + bucket ? money>=2 + money-2
Buy a sponge for <money>1 => shop ? shop:sponge ?! sponge + sponge ? money>=1 + money-1
Buy a box of soap for <money>1 => shop ? shop:soap ?! soap + soap ? money>=1 + money-1
Buy some spray paint for <money>3 => shop ? shop:paint ?! paint + paint ? money>=3 + money-3
Go back to the street => street

# generate_school
You walk over to the school.
Continue => school

# school
There is a donation box by the school entrance. They are raising money for roof repairs.
Donate <money>1 => school ? money>=1 + money-1 + good
Donate <money>2 => school ? money>=2 + money-2 + good+3
Donate <money>5 => school ? money>=5 + money-5 + good+8
Go back to the street => street

# generate_blacksmith
~ bs:nohammer ~ bs:nails
+ bs:nohammer ? __R__>0.2
+ bs:nails ? __R__>0.2
You walk over to the blacksmith's shop.
Continue => blacksmith

# blacksmith
You are at the blacksmith's shop.
The blacksmith has had their hammer stolen. ? bs:nohammer
The blacksmith is selling nails. ? bs:nails
Give the blacksmith a hammer => blacksmith ? bs:nohammer ~  bs:nohammer ? hammer ~ hammer + good+2
Buy 20 nails for <money>1 => blacksmith ? bs:nails ? money>=1 + money-1 + nails+20
Go back to the street => street

# generate_villagegreen
~ vg:crisps ~ vg:dogwalker ~ vg:hammer
+ vg:crisps ? __R__>0.8
+ vg:dogwalker ? __R__>0.5
+ vg:hammer ? __R__ > 0.6 ?! hammer
You walk onto the village green.
Continue => villagegreen

# villagegreen
You are on the village green.
There is a crisp packet next to you. ? vg:crisps
Someone is walking their dog on the green. ? vg:dogwalker
Someone is around the green with a hammer. ? vg:hammer
Pick up the crisp packet => villagegreen ? vg:crisps ~ vg:crisps + crisp_packet + good
Talk to the dog walker => dogtalker ? vg:dogwalker
Talk to the dog => dogtalk ? vg:dogwalker
Talk to the hammer holder => hammertalk ? vg:hammer
Go back to the street => street

# dogtalk
"Woof!" says the dog.
Continue => villagegreen

# dogtalker
"My dog is friendly, why don't you say hello," says the dog walker.
Continue => villagegreen

# hammertalk
"I stole this hammer from the blacksmith," laugh the holder of the hammer.
Take the hammer => takehammer
Continue => villagegreen

# takehammer
You take the hammer. The hammer holder runs away. + hammer ~ vg:hammer
Continue => villagegreen

% money
Â£
__NUMBER__(10)

% crisp_packet
Crisp packets ? crisp_packet>0
__NUMBER__

% hammer
A hammer

% saw
A saw

% nails
__NUMBER__
Nails ? nails

% screwdriver
A screwdriver

% bucket
A bucket

% sponge
A sponge

% soap
Soap

% paint
Spray paint

% good
__NUMBER__

% tired
__NUMBER__
