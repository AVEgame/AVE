== Welcome to Lower Mudville ==
-- Explore this randomly generated village. What will you discover? --
** Matthew Scroggs **
@@ 3 @@
vv 1 vv
:: 1.9 ::

# start
You are standing in the road near a sign that says "Welcome to Lower Mudville".
Enter Lower Mudville => generate_street

# generate_street
+ tired
~ street:bin ~ street:crisps ~ street:police
~ street:left:house ~ street:left:field ~ street:left:wall
~ street:right:villagehall ~ street:right:house
~ leftgen ~ rightgen
+ street:bin ? __R__ > 0.5
+ street:crisps ? __R__ > 0.5
+ street:police ? __R__ > 0.9
+ street:left:house ? __R__ > 0.5
+ street:left:field ?! street:left:house ? __R__ > 0.5
+ street:left:wall ?! street:left:house ?! street:left:field
+ street:right:villagehall ? __R__ > 0.95 
+ street:right:house ?! street:right:villagehall ?! street:left:house ? __R__ > 0.6
You walk along the street.
Continue => street

# street
You are standing in the street.
There is a bin on the pavement near you. ? street:bin
There is a crisp packed on the road next to you. ? street:crisps
There is a police officer nearby. ? street:police
There is a house to your left. ? street:left:house
There is a field to your left. ? street:left:field
There is a wall to your left. ? street:left:wall
There is a village hall to your right ? street:right:villagehall
There is a house to your right. ? street:right:house
You are very tired. ? tired>20
Put the crisp packet(s) in the bin => street ? street:bin ? crisp_packet + good+crisp_packet + crisp_packet=0
Pick up the crisp packet => street ? street:crisps + crisp_packet ~ street:crisps
Walk along the street => generate_street
Go to the wall => generate_wall ? street:left:wall ?! leftgen + leftgen
Go into the house => generate_house ? street:left:house ?! leftgen + leftgen
Go into the house => generate_house ? street:right:house ?! rightgen + rightgen
Go into the field => generate_field ? street:left:field ?! leftgen + leftgen
Go into the village hall => generate_village_hall ? street:right:villagehall ?! rightgen + rightgen
Go back to the wall => wall ? street:left:wall ? leftgen
Go back into the house => house ? street:left:house ? leftgen
Go back into the house => house ? street:right:house ? rightgen
Go back into the field => field ? street:left:field ? leftgen
Go back into the village hall => village_hall ? street:right:villagehall ? rightgen
Have a nap in the street => streetnap ? tired>20
Talk to the police officer => policetalk ? street:police

# generate_wall
~ wall:unclean ~ wall:dented
~ wall:unclean ? __R__ > 0.6
You walk over to the wall.
Continue => wall

# wall
You are standing by a wall.
The wall is covered in grafitti. ? wall:unclean
The wall is covered in dents made by a hammer. ? wall:dented
Wash the wall => wash ? soap ? bucket ? sponge ? wall:unclean ~ wall:unclean
Cover the wall in grafitti => wall ? paint ?! wall:unclean + wall:unclean + good-7 ~ paint
Put dents in the wall with the hammer => wall ? hammer ?! wall:dented + wall:dented + good-5
Go back to the street => street 

# wash
You use the soap, bucket and sponge to wash the wall. + good+5
Continue => wall

# policetalk
"Good day!" says the police officer. ? good > -10 + good
"I'm looking for ? good > -10 ? (!police:hammer !police:nails)
a hammer ? good > -10 ?! police:hammer
and ? good > -10 ?! police:hammer ?! police:nails
some nails ? good > -10 ?! police:nails
to fix the village notice board. Do you have any?" ? good > -10 ? (!police:hammer !police:nails)
"You've been making a mess of our village," say the police officer. "You are under arrest." ? good <= -10
Stop talking to the police officer => street ? good > -10
Give the police officer a hammer ? good > -10 ?! police:hammer + police:hammer ? hammer ~ hammer + good+5
Give the police officer some nails ? good > -10 ?! police:nails + police:nails ? nails ~ nails + good+5
Continue => arrest ? good <= -10

# arrest
You are in prison.
Continue => __GAMEOVER__

# streetnap
You have a nap in the road. + tired=0
You wake up to find there is a queue of cars waiting for you to move. The drivers look annoyed. + good-4 ? __R__ > 0.3
Continue => street

# generate_village_hall
~ vhall:meeting
+ vhall:meeting ? __R__ > 0.65
You walk into the village hall.
Continue => village_hall

# village_hall
You are standing in the village hall.
There is a parish council meeting taking place. ? vhall:meeting
Talk to the parish councillors => council ? vhall:meeting
Go back to the street => street

# council
As you approach the councillors, one of them points at you. ? good >= 10
"You have done many kinds deeds for this village. I nominate you for Mayor." ? good >= 10
"You have been wreaking havoc in our village. You are banished!"" ? good >= 10
"Can't you see we're busy," barks one of the councillors, "you'll have to come back later." ? good < 10 ~ good
Go back to the street => street ? good < 10 ? good > -10
Continue => mayor ? good >= 10
Continue => banished ? good <= -10

# mayor
You are mayor of Lower Mudville.
Continue => __WINNER__

# banished
You are banished from Lower Mudville.
Continue => __GAMEOVER__

# generate_house
~ house:angry ~ house:toolbox ~ house:toolhammer ~ house:tea ~ house:toolsaw ~ house:toolnails
~ house:toolscrewdriver
~ house:soap ~ house:bucket ~ house:sponge
+ house:angry ? __R__ > 0.9
+ house:tea ? __R__ > 0.75 ?! house:angry
+ house:toolbox ? __R__ > 0.7 ?! house:angry
+ house:toolhammer ? __R__ > 0.4 ? house:toolbox ?! house:angry
+ house:toolsaw ? __R__ > 0.4 ? house:toolbox ?! house:angry
+ house:toolnails ? __R__ > 0.4 ? house:toolbox ?! house:angry
+ house:toolscrewdriver ? __R__ > 0.4 ? house:toolbox ?! house:angry
+ house:soap ? __R__ > 0.5 ?! house:angry ?! soap
+ house:bucket ? __R__ > 0.5 ?! house:angry ?! bucket
+ house:sponge ? __R__ > 0.5 ?! house:angry ?! sponge
You walk into the house.
Continue => house

# house
You are standing in a house.
The owner of the house angrily tells you to get out of their house. ? house:angry ~ good
There is a cup of tea on the table. ? house:tea
There is a box of soap on the table. ? house:soap
There is a bucket by the table. ? house:bucket
There is a sponge on the table. ? house:sponge
There is a toolbox on the table. ? house:toolbox
Drink the tea => drinktea ? house:tea
Pick up the soap => house ? house:soap ~ house:soap + soap
Pick up the bucket => house ? house:bucket ~ house:bucket + bucket
Pick up the sponge => house ? house:sponge ~ house:sponge + sponge
Look in the toolbox => toolbox ? house:toolbox
Go back to the street => street

# toolbox
You look in the toolbox.
It is empty. ?! house:toolhammer ?! house:toolsaw ?! house:toolnails ?! house:toolscrewdriver
There is a hammer. ? house:toolhammer ?! hammer
There is a saw. ? house:toolsaw ?! saw
There is are some nails. ? house:toolnails ?! nails
There is a screwdriver. ? house:toolscrewdriver ?! screwdriver
Pick up the hammer => toolbox ? house:toolhammer ?! hammer + hammer
Pick up the saw => toolbox ? house:toolsaw ?! saw + saw
Pick up the nails => toolbox ? house:toolnails ?! nails + nails
Pick up the screwdriver => toolbox ? house:toolscrewdriver ?! screwdriver + screwdriver
Continue => house

# drinktea
You drink the tea. It is delicious (but a little cold). ~ house:tea + tired=0
Continue => house

# generate_field
~ field:scarecrow ~ field:buckethead ~ field:scarecrowcutdown ~ field:crisps
+ field:scarecrow ? __R__ > 0.6
You walk into the field.
Continue => field

# field
You are standing in a field.
There is a crisp packed in the field next to you. ? field:crisps
There is a scarecrow standing in the field. ? field:scarecrow ?! field:scarecrowcutdown
There is a scarecrow lying in the field. ? field:scarecrow ? field:scarecrowcutdown
There is bucket on the scarecrow's head. ? field:scarecrow ? field:buckethead
Pick up the crisp packet => field ? field:crisps ~ field:crisps + crisp_packet + good
Put the bucket on the scarecrow's head => field ? field:scarecrow ? bucket ~ bucket + field:buckethead ?! field:buckethead + good-3
Use the saw to cut down the scarecrow => field ? field:scarecrow ? saw ?! field:scarecrowcutdown + field:scarecrowcutdown + good-6
Go back to the street => street


% crisp_packet
Crisp packets ? crisp_packet>0
__NUMBER__

% hammer
A hammer

% saw
A saw

% nails
Some nails

% screwdriver
A screwdriver

% bucket
A bucket

% sponge
A sponge

% soap
Soap

% paint
Spray paint

% good
__NUMBER__

% tired
__NUMBER__(0)
